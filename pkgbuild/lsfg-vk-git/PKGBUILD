# Modified from: https://github.com/CachyOS/CachyOS-PKGBUILDS/blob/master/lsfg-vk/PKGBUILD

_pkgname="lsfg-vk"
pkgname="$_pkgname-git"
pkgver=r140.f998647
pkgrel=1
pkgdesc="Lossless Scaling Frame Generation on Linux via DXVK/Vulkan"
arch=('x86_64' 'aarch64')
url="https://github.com/PancakeTAS/lsfg-vk"
license=('MIT')
depends=(
  vulkan-icd-loader
)
makedepends=(
  base-devel
  clang
  cmake
  git
  llvm
  ninja
  spirv-headers
  vulkan-headers
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
  "git+$url"
  "git+https://github.com/PancakeTAS/dxbc.git"
  "git+https://github.com/trailofbits/pe-parse"
)
sha256sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname" || exit 1

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgname" || exit 1

  git submodule init

  git config submodule.dxbc.url "file://$srcdir/dxbc"
  git config submodule.pe-parse.url "file://$srcdir/pe-parse"

  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/$_pkgname" || exit 1

  cmake -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DCMAKE_CXX_CLANG_TIDY="" \
    -DCMAKE_SHARED_LINKER_FLAGS="-Wl,-z,lazy" # fixes makepkg's default "-z,now" flag which strips out the necessary symbols

  cmake --build build
}

package() {
  cd "$srcdir/$_pkgname" || exit 1

  DESTDIR="$pkgdir/" cmake --install build --prefix=/usr
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
