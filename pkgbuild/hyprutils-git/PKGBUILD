_pkgname="hyprutils"
pkgname="$_pkgname-git"
pkgver=0.10.3.r1.g96df6f6
pkgrel=1
pkgdesc="Hyprland utilities library used across the ecosystem"
arch=('x86_64' 'aarch64')
url="https://github.com/hyprwm/hyprutils"
license=('BSD-3-Clause')
depends=(
  pixman libpixman-1.so
)
makedepends=(
  cmake
  git
)
provides=("$_pkgname=${pkgver/\.r*/}" "lib$_pkgname.so")
conflicts=("$_pkgname")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname" || exit 1

  printf "%s" "${ git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'; }"
}

prepare() {
  cd "$srcdir/$_pkgname" || exit 1

  cmake -W no-dev -D CMAKE_BUILD_TYPE=Release -D CMAKE_INSTALL_PREFIX=/usr -S . -B ./build
}

build() {
  cd "$srcdir/$_pkgname" || exit 1

  cmake --build ./build
}

package() {
  cd "$srcdir/$_pkgname" || exit 1

  DESTDIR="$pkgdir/" cmake --install build
  install -Dm644 "$srcdir/$_pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
