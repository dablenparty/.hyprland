_pkgname="hyprland-protocols"
pkgname="$_pkgname-git"
pkgver=0.7.0.r1.g3f3860b
pkgrel=1
pkgdesc="Wayland protocol extensions for Hyprland"
arch=('x86_64' 'aarch64')
url="https://github.com/hyprwm/hyprland-protocols"
license=('BSD-3-Clause')
makedepends=(
  cmake
  git
)
provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname" || exit

  printf "%s" "${ git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'; }"
}

prepare() {
  cd "$srcdir/$_pkgname" || exit

  cmake -W no-dev \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -S . \
    -B ./build
}

build() {
  cd "$srcdir/$_pkgname" || exit

  cmake --build ./build
}

package() {
  cd "$srcdir/$_pkgname" || exit

  DESTDIR="$pkgdir/" cmake --install build
}
