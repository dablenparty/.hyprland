_pkgname=hyprland
pkgname="${_pkgname}-git"
pkgver=0.52.0.r84.g2b0fd41
pkgrel=1
pkgdesc="Hyprland provides the latest Wayland features, dynamic tiling, all the eyecandy, powerful plugins and much more, while still being lightweight and responsive"
arch=(x86_64)
url="https://hyprland.org/"
license=('BSD-3')
depends=(
  aquamarine-git libaquamarine.so
  cairo
  egl-wayland
  gcc-libs
  glib2 libgio-2.0.so libgobject-2.0.so
  glslang
  hyprcursor-git libhyprcursor.so
  hyprgraphics-git libhyprgraphics.so
  hyprland-guiutils-git
  hyprlang-git libhyprlang.so
  hyprpolkitagent-git
  hyprutils-git libhyprutils.so
  # Required for AQ & some hypr* packages
  hyprwayland-scanner-git
  libdisplay-info libdisplay-info.so
  libdrm
  libglvnd libEGL.so libGLESv2.so libOpenGL.so
  libinput
  libliftoff libliftoff.so
  libx11
  libxcb
  libxcomposite
  libxcursor
  libxfixes
  libxkbcommon libxkbcommon.so
  libxrender
  mesa
  muparser
  opengl-driver
  pango libpango-1.0.so libpangocairo-1.0.so
  pixman libpixman-1.so
  qt5-wayland
  qt6-wayland
  re2 libre2.so
  seatd libseat.so
  systemd-libs libsystemd.so
  tomlplusplus libtomlplusplus.so libudev.so
  util-linux-libs libuuid.so
  uwsm
  wayland libwayland-client.so libwayland-server.so
  wayland-protocols
  wl-clipboard
  xcb-proto
  xcb-util
  xcb-util-errors
  xcb-util-image
  xcb-util-keysyms
  xcb-util-renderutil
  xcb-util-wm
  xdg-desktop-portal-gtk-git
  xdg-desktop-portal-hyprland-git
  xorg-xwayland
)
makedepends=(
  cmake
  git
  glaze
  hyprland-protocols-git
  meson
  ninja
  xorgproto
)
optdepends=(
  'cmake: to build and install plugins using hyprpm'
  'cpio: to build and install plugins using hyprpm'
  'glaze: to build and install plugins using hyprpm'
  'hyprland-qt-support: QML style for QT6 hypr* apps'
  'hyprqt6engine: QT6 theme provider compatible with KDE/KColorScheme'
  'kitty: default terminal emulator'
  'meson: to build and install plugins using hyprpm'
  'nwg-look: GTK GUI for customizing GTK apps'
  'yazi: terminal-based file manager'
)
provides=("$_pkgname=${pkgver/\.r*/}" "wayland-compositor")
conflicts=(hyprland)
source=(
  "git+https://github.com/hyprwm/$_pkgname.git"
  "git+https://github.com/hyprwm/hyprland-protocols"
  "git+https://github.com/canihavesomecoffee/udis86"
  "git+https://github.com/wolfpld/tracy"
)
noextract=()
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname" || exit

  # Git, tags available
  printf "%s" "${ git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'; }"
}

prepare() {
  cd "$srcdir/$_pkgname" || exit

  git submodule init

  git config submodule.hyprland-protocols.url "file://$srcdir/subprojects/hyprland-protocols"
  git config submodule.udis86.url "file://$srcdir/subprojects/udis86"
  git config submodule.tracy.url "file://$srcdir/subprojects/tracy"

  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/$_pkgname" || exit

  # NOTE: this does manpages too
  make all PREFIX=/usr
}

package() {
  cd "$srcdir/$_pkgname" || exit
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}/"
}
